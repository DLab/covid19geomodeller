%params:  'alfa','beta', 'N','E_0','I_0','R_0', 't_EI','t_IR','r_RS', 'S_f','E_f','I_f','R_f', 'k_I','k_R','r_vac'

%param: 'alfa'	1.0
%param: 'beta'	0.2
%param: 'N'		100000
# %param: 'S_0'	
%param: 'E_0'	0
%param: 'I_0'	50
%param: 'R_0'	0
%param: 't_EI'	5
%param: 't_IR'	10
%param: 'r_RS'	0.0
%param: 'S_f'	0
%param: 'E_f'	0
%param: 'I_f'	0
%param: 'R_f'	0
%param: 'k_I'	1.0
%param: 'k_R'	1.0
%param: 'r_vac'	0.1


%const: 'r_EI' 1.0/'t_EI'
%const: 'r_IR' 1.0/'t_IR'

%var: 'vacc-rate' {[sum] ((1+x*x)/80.0)} P(vac~F,age~{x})

#main agent person
%agent: P(s~S~E~I~R,age~[0,80],vac~F~T)
%token: new_E
%token: new_I
%token: new_R
%token: new_S
%token: new_V


#rules
'expose' P(s~S,vac~F),P(s~I,age~{x}) -> P(s~E,...),P(...) | 1: new_E @ (3000-(x-35)^2)/2000.0 * 'alfa'*'beta'/'N'
'expose-vac' P(s~S,vac~T),P(s~I,age~{x}) -> P(s~E,...),P(...) | 1: new_E @ 'r_vac'*(3000-(x-35)^2)/2000.0 * 'alfa'*'beta'/'N'
'infect' P(s~E) -> P(s~I) | 1: new_I @ 'r_EI'
'remove' P(s~I,age) -> P(s~R,age) | 1: new_R @ 'r_IR'
'imm. loss' P(s~R) -> P(s~S) | 1: new_S @ 'r_RS'

'vaccinate' P(vac~F,age~{x}) -> P(vac~T,...) | 1: new_V @ ((1+x*x)/80.0) * 'N'*0.005/'vacc-rate'

#inits
%init: 'N'-'E_0'-'I_0'-'R_0' P(s~S,age~{[randN] 80})
%init: 'E_0' P(s~E,age~{[randN] 80})
%init: 'I_0' P(s~I,age~{[randN] 80})
%init: 'R_0' P(s~R,age~{[randN] 80})

%obs: 'Susceptible' P(s~S)
%obs: 'Exposed' P(s~E)
%obs: 'Infected' P(s~I,age)
%obs: 'Removed' P(s~R)
%obs: 'Vacc'	P(vac~F)


%obs: 'Daily Exposed' |new_E|
%obs: 'Daily Infected' |new_I|
%obs: 'Daily Removed' |new_R|
%obs: 'Daily Inmunized' |new_S|
%obs: 'Daily Vaccinated' |new_V|


%mod: repeat ([T] [mod] 1) = 0.001 do new_E <- 0; new_I <- 0; new_R <- 0; new_S <- 0; new_V <- 0 until [false]



