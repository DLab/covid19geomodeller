%params:  'alfa','beta', 'N','E_0','I_0','R_0', 't_EI','t_IR','r_RS', 'S_f','E_f','I_f','R_f', 'k_I','k_R'

%param: 'alfa'	1.0
%param: 'beta'	0.2
%param: 'N'		100000
# %param: 'S_0'	
%param: 'E_0'	0
%param: 'I_0'	1
%param: 'R_0'	0
%param: 't_EI'	5
%param: 't_IR'	10
%param: 'r_RS'	0.0
%param: 'S_f'	0
%param: 'E_f'	0
%param: 'I_f'	0
%param: 'R_f'	0
%param: 'k_I'	1.0
%param: 'k_R'	1.0


%const: 'r_EI' 1.0/'t_EI'
%const: 'r_IR' 1.0/'t_IR'

#main agent person
%agent: P(s~S~E~I~R)

%token: new_E
%token: new_I
%token: new_R
%token: new_S

#rules
'expose' P(s~S),P(s~I) -> P(s~E),P(s~I) | 1: new_E @ 'beta'/'N'
'infect' P(s~E) -> P(s~I) | 1: new_I @ 'r_EI'
'remove' P(s~I) -> P(s~R) | 1: new_R @ 'r_IR'
'imm. loss' P(s~R) -> P(s~S) | 1: new_S @ 'r_RS'

#inits
%init: 'N'-'E_0'-'I_0'-'R_0' P(s~S)
%init: 'E_0' P(s~E)
%init: 'I_0' P(s~I)
%init: 'R_0' P(s~R)

%obs: 'Susceptible' P(s~S)
%obs: 'Exposed' P(s~E)
%obs: 'Infected' P(s~I)
%obs: 'Removed' P(s~R)


%obs: 'Daily Exposed' |new_E|
%obs: 'Daily Infected' |new_I|
%obs: 'Daily Removed' |new_R|
%obs: 'Daily Inmunized' |new_S|


%mod: repeat ([T] [mod] 1) = 0.001 do new_E <- 0; new_I <- 0; new_R <- 0; new_S <- 0 until [false]



